plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
}

archivesBaseName = pluginId
group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven {
        name = 'gradle-plugins'
        url = 'https://plugins.gradle.org/m2'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile fileTree(dir: 'libraries', include: '*.jar')
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    
    shadow 'org.spongepowered:configurate-hocon:3.6.1'
    shadow ('net.dv8tion:JDA:4.2.0_214') {
        exclude module: 'opus-java'
    }

    // Database connectors
    shadow 'com.zaxxer:HikariCP:2.6.3'
    shadow 'org.mariadb.jdbc:mariadb-java-client:2.0.3'
    shadow 'com.h2database:h2:1.4.196'

    shadow group: 'com.google.code.gson', name: 'gson', version: '2.3.1'

    //Permission APIs
    compile 'me.lucko.luckperms:luckperms-api:4.4'
    compile 'net.luckperms:api:5.1'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.properties(
            version: version)
    filter(ReplaceTokens, tokens: [
            VERSION  : version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

shadowJar {
    archiveFileName = "${pluginId}-${pluginVersion}.jar"
    configurations = [project.configurations.shadow] // ***
    relocate 'com.fasterxml.jackson',       'net.dirtcraft.libs.com.fasterxml.jackson'
    relocate 'com.google',                  'net.dirtcraft.libs.com.google'
    relocate 'com.iwebpp',                  'net.dirtcraft.libs.com.iwebpp'
    relocate 'com.neovisionaries.ws',       'net.dirtcraft.libs.com.neovisionaries.ws'
    relocate 'com.typesafe',                'net.dirtcraft.libs.com.typesafe'
    relocate 'com.zaxxer',                  'net.dirtcraft.libs.com.zaxxer'
    relocate 'gnu.trove',                   'net.dirtcraft.libs.gnu.trove'
    relocate 'javax.annotation',            'net.dirtcraft.libs.javax.annotation'
    relocate 'me.vankka',                   'net.dirtcraft.libs.me.vankka'
    relocate 'net.dv8tion.jda',             'net.dirtcraft.libs.net.dv8tion.jda'
    relocate 'net.kyori',                   'net.dirtcraft.libs.net.kyori'
    relocate 'ninja.leaping',               'net.dirtcraft.libs.ninja.leaping'
    relocate 'okhttp3',                     'net.dirtcraft.libs.okhttp3'
    relocate 'okio',                        'net.dirtcraft.libs.okio'
    relocate 'org.apache.commons',          'net.dirtcraft.libs.apache.commons'
    relocate 'org.apache.http',             'net.dirtcraft.libs.org.apache.http'
    relocate 'org.json',                    'net.dirtcraft.libs.org.json'
    relocate 'com.google.code.gson',       'net.dirtcraft.libs.com.google.code.gson'
}
build.dependsOn(shadowJar)
compileJava.options.encoding = 'UTF-8'
